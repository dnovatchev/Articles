declare namespace f = "http://www.w3.org/2005/xpath-functions-2025";
declare record f:generator 
   ( initialized as xs:boolean,
     end-reached as xs:boolean,
     get-current as %method fn() as item()*,
     move-next as %method fn(*) (: as f:generator, :),
     to-array := %method fn() as array(*)
     {
       while-do( [., []],
                function( $inArr) 
                { $inArr(1)?initialized and not($inArr(1)?end-reached) },                 
                function($inArr) 
                { array{$inArr(1)?move-next(), 
                        array:append($inArr(2), $inArr(1)?get-current())
                       } 
                }         
       ) (2)
     },
     
     take := %method fn($n as xs:integer) as f:generator
     {
      let $gen := if(not(?initialized)) then ?move-next()
                    else .
       return
         if($gen?end-reached or $n le 0) then $gen?empty-generator()
          else
            let $current := $gen?get-current(),
                $newResultGen := map:put(., "get-current", %method fn(){$current}),
                $nextGen := $gen?move-next()
             return
               if($nextGen?end-reached) then $newResultGen
                 else
                   let
                       $newResultGen2 :=  map:put($newResultGen, "move-next", %method fn() {$nextGen?take($n -1)}) 
                     return
                       $newResultGen2
      },
      
      take-while := %method fn($pred as function(item()*) as xs:boolean) as f:generator
      {
        let $gen := if(not(?initialized)) then ?move-next()
                      else .
         return
           if($gen?end-reached) then $gen?empty-generator()
            else      
              let $current := $gen?get-current()
                return
                  if(not($pred($current))) then $gen?empty-generator()
                  else
                    let $newResultGen := map:put(., "get-current", %method fn(){$current}),
                        $nextGen := ?move-next()
                     return
                        if($nextGen?end-reached) then $newResultGen
                        else
                          let $newResultGen2 :=  map:put($newResultGen, "move-next", %method fn() {$nextGen?take-while($pred)}) 
                           return $newResultGen2  
      },
     
     skip-strict := %method fn($n as xs:nonNegativeInteger, $issueErrorOnEmpty as xs:boolean) as f:generator
     {
            if($n eq 0) then .
              else if(?end-reached) 
                     then if($issueErrorOnEmpty)
                           then error((), "Input Generator too-short") 
                           else ?empty-generator()
              else 
                let $gen := if(not(?initialized)) then ?move-next()
                             else .
                  return
                    if(not($gen?end-reached)) then $gen?move-next()?skip-strict($n -1, $issueErrorOnEmpty)
                      else $gen?empty-generator()                 

     },
     skip := %method fn($n as xs:nonNegativeInteger) as f:generator
     {
       ?skip-strict($n, false())
     },
     
     skip-while := %method fn($pred as function(item()*) as xs:boolean) as f:generator
     {
        let $gen := if(not(?initialized)) then ?move-next()
                      else .
         return
           if($gen?end-reached) then $gen?empty-generator()
            else
              let $current := $gen?get-current()
               return
                 if(not($pred($current))) then $gen
                  else $gen?move-next()?skip-while($pred)                    
     },
     
     some := %method fn() as xs:boolean
     {
       ?initialized and not(?end-reached)
     },
     
     some-where := %method fn($pred) as xs:boolean
     {
       ?filter($pred)?some()
     },
     
     first-where := %method fn($pred) as item()*
     {
       ?filter($pred)?head()
     },     
     
     subrange := %method fn($m as xs:positiveInteger, $n as xs:integer) as f:generator
     {
       ?skip($m - 1)?take($n - $m + 1)
     },
     
     chunk := %method fn($size as xs:positiveInteger) as f:generator
     {
        let $gen := if(not(?initialized)) then ?move-next()
                      else .
         return
           if($gen?end-reached) then $gen?empty-generator()
           else
             let $thisChunk := $gen?take($size)?to-array(),
                 $cutGen := $gen?skip($size),
                 $resultGen := $gen => map:put("get-current", %method fn(){$thisChunk})
                                    => map:put("move-next", %method fn(){$cutGen?chunk($size)})
              return $resultGen
     },
     
     head := %method fn() as item()* {?take(1)?get-current()},
     tail := %method fn() as f:generator {?skip(1)},
     
     at := %method fn($ind as xs:nonNegativeInteger) as item()* {?subrange($ind, $ind)?get-current()},
     
     contains := %method fn($value as item()*) as xs:boolean
     {
       let $gen := if(not(?initialized)) then ?move-next()
                     else .
        return
          if($gen?end-reached) then false()
           else
             let $current := $gen?get-current()
               return
                  if(deep-equal($current, $value)) then true()
                   else $gen?move-next()?contains($value)                  
     },
           
     for-each := %method fn($fun as function(*)) as f:generator
     {
      let $gen := if(not(?initialized)) then ?move-next()
                    else .        
       return
         if(?end-reached) then ?empty-generator()
          else
           let $current := $fun(?get-current()),
                $newResultGen := map:put(., "get-current", %method fn(){$current}),
                $nextGen := ?move-next()
            return
              if($nextGen?end-reached) then $newResultGen
                else
                  let $newResultGen2 :=  map:put($newResultGen, "move-next", %method fn() {$nextGen?for-each($fun)}) 
                     return
                       $newResultGen2                    
      },
      
      for-each-pair := %method fn($gen2 as f:generator, $fun as function(*)) as f:generator
      {
        let $gen := if(not(?initialized)) then ?move-next()
                    else .,
            $gen2 := if(not($gen2?initialized)) then $gen2?move-next()
                    else $gen2
         return
            if(?end-reached or $gen2?end-reached) then ?empty-generator() 
             else  
               let $current := $fun(?get-current(), $gen2?get-current()),
                   $newResultGen := map:put(., "get-current", %method fn(){$current}),
                   $nextGen1 := ?move-next(),
                   $nextGen2 := $gen2?move-next()
                return
                   if($nextGen1?end-reached or $nextGen2?end-reached) then $newResultGen
                     else
                       let $newResultGen2 := map:put($newResultGen, "move-next", %method fn(){$nextGen1?for-each-pair($nextGen2, $fun)})
                         return
                           $newResultGen2                        
      },
      
      zip := %method fn($gen2 as f:generator) as f:generator
      {
        ?for-each-pair($gen2, fn($x1, $x2){[$x1, $x2]})
      },

      concat := %method fn($gen2 as f:generator) as f:generator
      {
        let $gen := if(not(?initialized)) then ?move-next()
                    else .,
            $gen2 := if(not($gen2?initialized)) then $gen2?move-next()
                    else $gen2,
            $resultGen := if($gen?end-reached) then $gen2
                            else if($gen2?end-reached) then $gen
                            else
                              $gen  => map:put(  "move-next", 
                                                %method fn()
                                                 {
                                                 let $nextGen := $gen?move-next()
                                                   return 
                                                     $nextGen?concat($gen2)
                                                 }
                                              )                                   
        return 
           $resultGen            
      },

      append := %method fn($value as item()*) as f:generator
      {
        let $gen := if(not(?initialized)) then ?move-next()
                    else .,
            $genSingle := $gen => map:put("get-current", %method fn(){$value})
                               => map:put("move-next", %method fn(){?empty-generator()})
                               => map:put("end-reached", false())
         return
           $gen?concat($genSingle)                    
      },
      
      prepend := %method fn($value as item()*) as f:generator
      {
                let $gen := if(not(?initialized)) then ?move-next()
                    else .,
                    $genSingle := $gen => map:put("get-current", %method fn(){$value})
                                       => map:put("move-next", %method fn(){?empty-generator()})
         return
           $genSingle?concat($gen)  
      },
      
      insert-at := %method fn($pos as xs:positiveInteger, $value as item()*) as f:generator
      {
        let $genTail := ?skip-strict($pos - 1, true())
         return
            if($pos gt 1)
              then ?take($pos - 1)?append($value)?concat($genTail)
              else $genTail?prepend($value)               
      },
      
      remove-at := %method fn($pos as xs:nonNegativeInteger) as f:generator
      {
        let $genTail := ?skip-strict($pos, true())
          return
            if($pos gt 1)
              then ?take($pos - 1)?concat($genTail)
              else $genTail
      },
      
      remove-where := %method fn($predicate as function(item()*) as xs:boolean) as f:generator
      {
        let $gen := if(not(?initialized)) then ?move-next()
                      else .
          return
            $gen?filter(fn($x){not($predicate($x))})  
      },
      
      distinct %method fn() as f:generator
      {
        let $gen := if(not(?initialized)) then ?move-next()
                      else .
         return
           if($gen?end-reached) then $gen
           else
             let $priorValue := ?get-current()
               return
                 $gen => map:put("move-next", %method fn(){$gen?tail()?remove-where(fn($x){deep-equal($priorValue, $x)})?distinct()})  
      },
    
      replace := %method fn($funIsMatching as function(item()*) as xs:boolean, $replacement as item()*) as f:generator
      {
        if(?end-reached) then .
          else
            let $current := ?get-current()
              return
                if($funIsMatching($current))
                  then let $nextGen := ?move-next()
                     return
                       . => map:put("get-current", %method fn() {$replacement})
                         => map:put("move-next", %method fn() { $nextGen } 
                                  )
                  else (: $current is not the match for replacement :)
                    let $nextGen := ?move-next()
                      return . => map:put("move-next", 
                                           %method fn()
                                           {
                                             let $intendedReplace := function($z) {$z?replace($funIsMatching, $replacement)}
                                              return
                                                if($nextGen?end-reached) then $nextGen
                                                else $intendedReplace($nextGen)
                                           }
                                        )
      },
      
      reverse := %method fn() as f:generator
      {
        if(?end-reached) then ?empty-generator()
          else
           let $current := ?get-current()
             return
               ?tail()?reverse()?append($current)
      },

      filter := %method fn($pred as function(item()*) as xs:boolean) as f:generator
      {
             if(?initialized and ?end-reached) then ?empty-generator()
              else
                let $getNextGoodGen := function($gen as map(*), 
                                             $pred as function(item()*) as xs:boolean)
                   {
                      if($gen?end-reached) then $gen?empty-generator()
                      else
                        let $mapResult := 
                              while-do(
                                       $gen,
                                       function($x) { not($x?end-reached) and not($pred($x?get-current()))},
                                       function($x) { $x?move-next() }
                                       )   
                        return 
                          if($mapResult?end-reached) then $gen?empty-generator()
                           else $mapResult                  
                   },
                   
                   $gen := if(?initialized) then . 
                             else ?move-next(),
                   $nextGoodGen := $getNextGoodGen($gen, $pred)
                return
                  if($nextGoodGen?end-reached) then $gen?empty-generator()
                  else
                    $nextGoodGen => map:put("move-next", 
                                            %method fn() 
                                              {
                                                let $nextGoodGen := $getNextGoodGen(?inputGen?move-next(), $pred)
                                                  return
                                                    if($nextGoodGen?end-reached) then $nextGoodGen?empty-generator()
                                                    else
                                                      map:put(map:put($nextGoodGen, "move-next", %method fn() {$nextGoodGen?move-next()?filter($pred)}),
                                                                      "inputGen", $nextGoodGen
                                                              )
                                               }
                                           )
                                   =>
                                     map:put("inputGen", $nextGoodGen)
        },     
        fold-left := %method fn($init as item()*, $action as fn(*)) as item()*
        {
          if(?end-reached) then $init
            else ?tail()?fold-left($action($init, ?get-current()), $action)
        },
        
        fold-right := %method fn($init as item()*, $action as fn(*)) as item()*
        {
          if(?end-reached) then $init
            else $action(?head(), ?tail()?fold-right($init, $action))
        },
        
        fold-lazy := %method fn($init as item()*, $action as fn(*), $shortCircuitProvider as function(*)) as item()*
        {
          if(?end-reached) then $init
          else
           let $current := ?get-current()
             return
               if(function-arity($shortCircuitProvider($current, $init)) eq 0)
                 then $shortCircuitProvider($current, $init)()
                 else $action($current, ?move-next()?fold-lazy($init, $action, $shortCircuitProvider))
        },
        
        scan-left := %method fn($init as item()*, $action as fn(*)) as f:generator
        {
          let $resultGen := ?empty-generator() 
                                => map:put("end-reached", false())
                                => map:put("get-current", %method fn(){$init})
           return
             if(?end-reached) 
               then $resultGen => map:put("move-next", %method fn(){?empty-generator()})
               else
                 let $resultGen := $resultGen => map:put("get-current", %method fn(){$init}),
                     $partialFoldResult := $action($init, ?get-current())
                   return
                     let $nextGen := ?move-next()
                      return
                        $resultGen => map:put("move-next", %method fn()
                                              { 
                                                  $nextGen?scan-left($partialFoldResult, $action)
                                               }
                                              )            
        },
      
        scan-right := %method fn($init as item()*, $action as fn(*)) as f:generator
        {
          ?reverse()?scan-left($init, $action)?reverse()                         
        },
        
        make-generator := %method fn($provider as function(*)) as f:generator
        {
         let $gen := if(not(?initialized)) then ?move-next()
                    else .,
              $nextDataItemGetter := $provider(0),
              $nextGen := if(not($nextDataItemGetter instance of function(*))) then $gen?empty-generator()  
                           else $gen?empty-generator()
                            => map:put("numDataItems", 1)
                            => map:put("current", $nextDataItemGetter())
                            => map:put("end-reached", false())
                            => map:put("get-current", %method fn() {?current})
                            => map:put("move-next",  
                                       %method fn() 
                                        {
                                          let $nextDataItemGetter := $provider(?numDataItems)
                                            return
                                              if(not($nextDataItemGetter instance of function(*))) then ?empty-generator()
                                              else
                                                . => map:put("current", $nextDataItemGetter())
                                                  => map:put("numDataItems", ?numDataItems + 1)
                                        }
                                       )
           return $nextGen                                                  
        },
        
        make-generator-from-array := %method fn($input as array(*)) as f:generator
        {
          let $size := array:size($input),
              $arrayProvider := fn($ind as xs:integer)
                                {
                                  if($ind +1 gt $size) then -1
                                   else fn(){$input($ind + 1)}
                                }
           return ?make-generator($arrayProvider)
        },
        
        make-generator-from-sequence := %method fn($input as item()*) as f:generator
        {
          let $size := count($input),
              $seqProvider := fn($ind as xs:integer)
                                {
                                  if($ind +1 gt $size) then -1
                                   else fn(){$input[$ind + 1]}
                                }
           return ?make-generator($seqProvider)
        },
        
        make-generator-from-map := %method fn($inputMap as map(*)) as f:generator
        {
          let $keys := map:keys($inputMap),
              $size := map:size($inputMap),
              $mapProvider := fn($ind as xs:integer)
              {
                if($ind +1 gt $size) then -1
                  else fn() 
                       {
                         let $key := $keys[$ind + 1]
                          return
                            ( $key, [ $inputMap($key) ])
                       }
              }
            return
              ?make-generator($mapProvider)
        },
        
        to-sequence := %method fn() as item()* {?to-array() => array:items()},     
        
        to-map := %method fn() as map(*)
        {
          let $genPairs := ?for-each(fn($x)
                           {
                             let $key := head($x),
                                 $tail := tail($x),
                                 $value := if($tail instance of array(*)) 
                                           then for $ind in 1 to array:size($tail)
                                                 return $tail($ind)
                                           else $tail
                             return
                               map:pair($key, $value)
                           }
                         ) 
           return
             map:of-pairs($genPairs?to-sequence())

        },
        
        empty-generator := %method fn() as f:generator
        {
          . => map:put("initialized", true()) => map:put("end-reached", true())
            => map:put("get-current", %method fn() {error((),"get-current() called on an empty-generator")})
            => map:put("move-next", %method fn() {error((),"move-next() called on an empty-generator")})
        },      
     *
   );

let $gen2ToInf := f:generator(initialized := true(), end-reached := false(), 
                              get-current := %method fn(){?last +1},
                              move-next := %method fn()
                              {
                                if(not(?initialized))
                                  then map:put(., "initialized", true())
                                  else map:put(., "last", ?last + 1)
                              },
                              options := {"last" : 1}
                             ),
    $genN := $gen2ToInf?for-each(fn($n) {$n - 1}),
    $gen0toInf := $gen2ToInf?for-each(fn($n) {$n - 2}),
    $double := fn($n) {2*$n},
    $sum2 := fn($m, $n) {$m + $n},
    $product := fn($m, $n) {$m * $n},
    $factorial := fn($n) {fold-left(1 to $n, 1, $product)}
  return    
  (
    "$gen2ToInf?take(3)?to-array()",
    $gen2ToInf?take(3)?to-array(),
    "================",    
    "$gen2ToInf?take(3)?skip(2)?get-current()",
    $gen2ToInf?take(3)?skip(2)?get-current(),
    (: $gen2ToInf?take(3)?move-next()?move-next()?move-next()?get-current(), :)
    "================",
    "$gen2ToInf?get-current()",
    $gen2ToInf?get-current(),
    "$gen2ToInf?move-next()?get-current()",
    $gen2ToInf?move-next()?get-current(),
    "================",
    "$gen2ToInf?take(5) instance of f:generator",
    $gen2ToInf?take(5) instance of f:generator,
    "==>  $gen2ToInf?skip(7) instance of f:generator",
    $gen2ToInf?skip(7) instance of f:generator,  
    "================",
    "$gen2ToInf?subrange(4, 6)?get-current()",
    $gen2ToInf?subrange(4, 6)?get-current(), 
    "$gen2ToInf?subrange(4, 6)?move-next()?get-current()",
    $gen2ToInf?subrange(4, 6)?move-next()?get-current(),
    "$gen2ToInf?subrange(4, 6)?move-next()?move-next()?get-current()",
    $gen2ToInf?subrange(4, 6)?move-next()?move-next()?get-current(),
    (: $gen2ToInf?subrange(4, 6)?move-next()?move-next()?move-next()?get-current() :) (: Must raise error:)    
    "================",    
    "$gen2ToInf?subrange(4, 6)?head()",
    $gen2ToInf?subrange(4, 6)?head(),  
    "$gen2ToInf?subrange(4, 6)?tail()?head()",
    $gen2ToInf?subrange(4, 6)?tail()?head(),
    "$gen2ToInf?subrange(4, 6)?to-array()",
    $gen2ToInf?subrange(4, 6)?to-array(),
    "$gen2ToInf?head()",
    $gen2ToInf?head(),
    "==>  $gen2ToInf?tail()?head()",
    $gen2ToInf?tail()?head(),
    "================", 
    "$gen2ToInf?subrange(4, 6)?tail()?to-array()",
    $gen2ToInf?subrange(4, 6)?tail()?to-array(),
    "================",
    "$gen2ToInf?at(5)",
    $gen2ToInf?at(5), 
    "================",
    "$gen2ToInf?subrange(1, 5)?to-array()",
    $gen2ToInf?subrange(1, 5)?to-array(),
    "$gen2ToInf?subrange(1, 5)?for-each($double)?to-array()",
    $gen2ToInf?subrange(1, 5)?for-each($double)?to-array(),
    "$gen2ToInf?take(5)?for-each($double)?to-array()",
    $gen2ToInf?take(5)?for-each($double)?to-array(),
    "==>  $gen2ToInf?for-each($double)?take(5)?to-array()",
    $gen2ToInf?for-each($double)?take(5)?to-array(),
    "================",
    "$gen2ToInf?subrange(1, 5)?to-array()",
    $gen2ToInf?subrange(1, 5)?to-array(),
    "$gen2ToInf?subrange(6, 10)?to-array()",
    $gen2ToInf?subrange(6, 10)?to-array(),
    "$gen2ToInf?subrange(1, 5)?for-each-pair($gen2ToInf?subrange(6, 10), $sum2)?to-array()",
    $gen2ToInf?subrange(1, 5)?for-each-pair($gen2ToInf?subrange(6, 10), $sum2)?to-array(), 
    "==>  $gen2ToInf?for-each-pair($gen2ToInf, $sum2)?take(5)?to-array()",
    $gen2ToInf?for-each-pair($gen2ToInf, $sum2)?take(5)?to-array(),
    "================",
    "==>  $gen2ToInf?filter(fn($n){$n mod 2 eq 1})?get-current()",
    $gen2ToInf?filter(fn($n){$n mod 2 eq 1})?get-current(),
    "$gen2ToInf?filter(fn($n){$n mod 2 eq 1})?move-next()?get-current()",
    $gen2ToInf?filter(fn($n){$n mod 2 eq 1})?move-next()?get-current(),
    "================", 
    "$gen2ToInf?filter(fn($n){$n mod 2 eq 1})?take(10)?to-array()",
    $gen2ToInf?filter(fn($n){$n mod 2 eq 1})?take(10)?to-array(),  
    "================", 
    "$gen2ToInf?filter(fn($n){$n mod 2 eq 1})?take(10)?to-sequence()",
    $gen2ToInf?filter(fn($n){$n mod 2 eq 1})?take(10)?to-sequence(),
    "================", 
    "$gen2ToInf?take-while(fn($n){$n lt 11})?to-array()",
    $gen2ToInf?take-while(fn($n){$n lt 11})?to-array(), 
    "$gen2ToInf?take-while(fn($n){$n lt 2})?to-array()",
    $gen2ToInf?take-while(fn($n){$n lt 2})?to-array(), 
    "================", 
    "$gen2ToInf?skip-while(fn($n){$n lt 11})?take(5)?to-array()",
    $gen2ToInf?skip-while(fn($n){$n lt 11})?take(5)?to-array(),
    "==> $gen2ToInf?skip-while(fn($n){$n lt 2})",
    $gen2ToInf?skip-while(fn($n){$n lt 2}),
    "
     ==> $gen2ToInf?skip-while(fn($n){$n lt 2})?skip(1)",
    $gen2ToInf?skip-while(fn($n){$n lt 2})?skip(1),
(:    $gen2ToInf?skip-while(fn($x) {$x ge 2}) :) (: ?skip(1) :) 
    "$gen2ToInf?some()",
     $gen2ToInf?some(),
     "let $empty := $gen2ToInf?empty-generator()
      return $empty?some()",
     let $empty := $gen2ToInf?empty-generator()
      return $empty?some(),
    "================",
    "$gen2ToInf?take(5)?filter(fn($n){$n ge 7})?some()",
     $gen2ToInf?take(5)?filter(fn($n){$n ge 7})?some(),  
     "$gen2ToInf?take(5)?some-where(fn($n){$n ge 7})",
     $gen2ToInf?take(5)?some-where(fn($n){$n ge 7}), 
     "$gen2ToInf?take(5)?some-where(fn($n){$n ge 6})",
     $gen2ToInf?take(5)?some-where(fn($n){$n ge 6}),
     "$gen2ToInf?some-where(fn($n){$n ge 100})",
     $gen2ToInf?some-where(fn($n){$n ge 100}),
     "================",
     "$gen2ToInf?take(10)?take(11)?to-array()",
     $gen2ToInf?take(10)?take(11)?to-array(),
     "$gen2ToInf?take(10)?skip(10)?to-array()",
     $gen2ToInf?take(10)?skip(10)?to-array(),
     "$gen2ToInf?take(10)?skip(9)?to-array()",     
     $gen2ToInf?take(10)?skip(9)?to-array(),
     "$gen2ToInf?take(10)?subrange(3, 12)?to-array()",
     $gen2ToInf?take(10)?subrange(3, 12)?to-array(),
     "$gen2ToInf?take(10)?subrange(5, 3)?to-array()",
     $gen2ToInf?take(10)?subrange(5, 3)?to-array(),
     "================",
     "$gen2ToInf?take(100)?chunk(20)?get-current()",
      $gen2ToInf?take(100)?chunk(20)?get-current(),
      "==>  $gen2ToInf?chunk(20)?take(5)?to-array()",
      $gen2ToInf?chunk(20)?take(5)?to-array(),
     "================",
     "$gen2ToInf?take(100)?chunk(20)?move-next()?get-current()",
      $gen2ToInf?take(100)?chunk(20)?move-next()?get-current(),
     "$gen2ToInf?take(100)?chunk(20)?move-next()?move-next()?get-current()", 
      $gen2ToInf?take(100)?chunk(20)?move-next()?move-next()?get-current(),
     "$gen2ToInf?take(100)?chunk(20)?skip(1)?get-current()",      
      $gen2ToInf?take(100)?chunk(20)?skip(1)?get-current(),
     "================",      
     "$gen2ToInf?take(100)?chunk(20)?for-each(fn($genX){$genX})?to-array()",      
      $gen2ToInf?take(100)?chunk(20)?for-each(fn($genX){$genX})?to-array(),
     "================",  
     "$gen2ToInf?take(10)?chunk(4)?to-array()",
      $gen2ToInf?take(10)?chunk(4)?to-array(),
      "$gen2ToInf?take(10)?chunk(4)?for-each(fn($arr){array:size($arr)})?to-array()",
      $gen2ToInf?take(10)?chunk(4)?for-each(fn($arr){array:size($arr)})?to-array(),
     "================", 
     "$gen2ToInf?subrange(10, 15)?concat($gen2ToInf?subrange(1, 9))?to-array()",
     $gen2ToInf?subrange(10, 15)?concat($gen2ToInf?subrange(1, 9))?to-array(),
     "================", 
     "$gen2ToInf?subrange(1, 5)?append(101)?to-array()",
     $gen2ToInf?subrange(1, 5)?append(101)?to-array(),
     "$gen2ToInf?subrange(1, 5)?prepend(101)?to-array()",
     $gen2ToInf?subrange(1, 5)?prepend(101)?to-array(),
     "==>  $gen2ToInf?append(101)",
     $gen2ToInf?append(101),
     "$gen2ToInf?prepend(101)?take(5)?to-array()",
     $gen2ToInf?prepend(101)?take(5)?to-array(),
     "================", 
     "$gen2ToInf?subrange(1, 5)?zip($gen2ToInf?subrange(6, 10))?to-array()",
     $gen2ToInf?subrange(1, 5)?zip($gen2ToInf?subrange(6, 10))?to-array(),
     "$gen2ToInf?subrange(1, 5)?zip($gen2ToInf?subrange(10, 20))?to-array()",
     $gen2ToInf?subrange(1, 5)?zip($gen2ToInf?subrange(10, 20))?to-array(),
     "==>  $gen2ToInf?zip($gen2ToInf?skip(5))?take(10)?to-array()",
     $gen2ToInf?zip($gen2ToInf?skip(5))?take(10)?to-array(),
     "$gen2ToInf?subrange(1, 5)?zip($gen2ToInf?subrange(10, 20))?zip($gen2ToInf?subrange(30, 40))?to-array()",
     $gen2ToInf?subrange(1, 5)?zip($gen2ToInf?subrange(10, 20))?zip($gen2ToInf?subrange(30, 40))?to-array(),
     "================", 
     "$gen2ToInf?make-generator(fn($numGenerated as xs:integer)
                                 {if($numGenerated le 9) then fn() {$numGenerated + 1} else -1} 
                             )?to-array()",
     $gen2ToInf?make-generator(fn($numGenerated as xs:integer)
                                 {if($numGenerated le 9) then fn() {$numGenerated + 1} else -1} 
                             )?to-array(),
     "================", 
     "$gen2ToInf?make-generator-from-array([1, 4, 9, 16, 25])?to-array()",
      $gen2ToInf?make-generator-from-array([1, 4, 9, 16, 25])?to-array(),
      "$gen2ToInf?make-generator-from-sequence((1, 8, 27, 64, 125))?to-array()",
      $gen2ToInf?make-generator-from-sequence((1, 8, 27, 64, 125))?to-array(), 
     "================", 
     "$gen2ToInf?take(10)?insert-at(3, ""XYZ"")?to-array()",
      $gen2ToInf?take(10)?insert-at(3, "XYZ")?to-array(),
      "$gen2ToInf?take(10)?insert-at(1, ""ABC"")?to-array()",
      $gen2ToInf?take(10)?insert-at(1, "ABC")?to-array(),
      "$gen2ToInf?take(10)?insert-at(11, ""PQR"")?to-array()",
      $gen2ToInf?take(10)?insert-at(11, "PQR")?to-array(),
      "==>  $gen2ToInf?insert-at(3, ""XYZ"")?take(10)?to-array()", 
      $gen2ToInf?insert-at(3, "XYZ")?take(10)?to-array(),
     (: , $gen2ToInf?take(10)?insert-at(12, "GHI")?to-array() :)  (:  Must raise error "Input Generator too-short." :) 
     "================", 
     "$gen2ToInf?take(10)?remove-at(3)?to-array()",
      $gen2ToInf?take(10)?remove-at(3)?to-array(),
      "$gen2ToInf?take(10)?remove-at(1)?to-array()",
      $gen2ToInf?take(10)?remove-at(1)?to-array(),
      "$gen2ToInf?take(10)?remove-at(10)?to-array()",
      $gen2ToInf?take(10)?remove-at(10)?to-array(),
      "==>  $gen2ToInf?remove-at(3)?take(10)?to-array()",
      $gen2ToInf?remove-at(3)?take(10)?to-array(),
      (: , $gen2ToInf?take(10)?remove-at(11)?to-array() :)        (:  Must raise error "Input Generator too-short." :) 
     "================",
     "==>  $gen2ToInf?remove-where(fn($x){$x mod 3 eq 0})?take(10)?to-array()",
      $gen2ToInf?remove-where(fn($x){$x mod 3 eq 0})?take(10)?to-array(),    
     "================",
     "$gen2ToInf?make-generator-from-sequence((1,  3, 1, 2,  1, 2, 5, 2, 5))?distinct()?to-array()",
      $gen2ToInf?make-generator-from-sequence((1,  3, 1, 2,  1, 2, 5, 2, 5))?distinct()?to-array(),
      "$gen2ToInf?for-each(fn($n){$n idiv 10})?take(50)?distinct()?to-array()",
      $gen2ToInf?for-each(fn($n){$n idiv 10})?take(50)?distinct()?to-array(),
      "$gen2ToInf?for-each(fn($n){$n idiv 10})?take(100)?distinct()?to-array()",
      $gen2ToInf?for-each(fn($n){$n idiv 10})?take(100)?distinct()?to-array(),
      "==> $gen2ToInf?for-each(fn($n){$n idiv 10})?distinct()?take(100)?to-array()",
      $gen2ToInf?for-each(fn($n){$n idiv 10})?distinct()?take(100)?to-array(),
     "================",          
     "$gen2ToInf?take(10)?replace(fn($x){$x gt 4}, ""Replacement"")?to-array()",
      $gen2ToInf?take(10)?replace(fn($x){$x gt 4}, "Replacement")?to-array(),
      "$gen2ToInf?take(10)?replace(fn($x){$x lt 3}, ""Replacement"")?to-array()",
      $gen2ToInf?take(10)?replace(fn($x){$x lt 3}, "Replacement")?to-array(),
      "$gen2ToInf?take(10)?replace(fn($x){$x gt 10}, ""Replacement"")?to-array()",
      $gen2ToInf?take(10)?replace(fn($x){$x gt 10}, "Replacement")?to-array(),
      "$gen2ToInf?take(10)?replace(fn($x){$x gt 11}, ""Replacement"")?to-array()",
      $gen2ToInf?take(10)?replace(fn($x){$x gt 11}, "Replacement")?to-array(),
      "$gen2ToInf?take(10)?replace(fn($x){$x lt 2}, ""Replacement"")?to-array()",
      $gen2ToInf?take(10)?replace(fn($x){$x lt 2}, "Replacement")?to-array(),
      "==> $gen2ToInf?replace(fn($x){$x gt 4}, ""Replacement"")?take(10)?to-array()",
      $gen2ToInf?replace(fn($x){$x gt 4}, "Replacement")?take(10)?to-array(),
      "$gen2ToInf?replace(fn($x){$x lt 3}, ""Replacement"")?take(10)?to-array()",
      $gen2ToInf?replace(fn($x){$x lt 3}, "Replacement")?take(10)?to-array(),
    (:  
      Will result in endless loop:
      
      , "==>  ==>  ==>  $gen2ToInf?replace(fn($x){$x lt 2}, ""Replacement"")?take(10)?to-array() <==  <==  <==",
      $gen2ToInf?replace2(fn($x){$x lt 2}, "Replacement")?take(10)?to-array() 
    :)
    "================",
    "$gen2ToInf?empty-generator()?reverse()?to-array()",
    $gen2ToInf?empty-generator()?reverse()?to-array(),
    "$gen2ToInf?empty-generator()?append(2)?reverse()?to-array()",
    $gen2ToInf?empty-generator()?append(2)?reverse()?to-array(),
    "$gen2ToInf?take(10)?reverse()?to-array()",
    $gen2ToInf?take(10)?reverse()?to-array(),
    "================",
    "$genN?take(10)?contains(3)",
    $genN?take(10)?contains(3),
    "$genN?take(10)?contains(20)",
    $genN?take(10)?contains(20),
    "$genN?take(10)?contains(1)",    
    $genN?take(10)?contains(1), 
    "$genN?take(10)?contains(10)",     
    $genN?take(10)?contains(10),  
    "$genN?take(10)?contains(0)",
    $genN?take(10)?contains(0), 
    "$genN?take(10)?contains(11)",        
    $genN?take(10)?contains(11),
    "==> $genN?contains(15)",    
    $genN?contains(15), 
    "================",
    "$gen2ToInf?take(5)?fold-left(0, fn($x, $y){$x + $y})",
    $gen2ToInf?take(5)?fold-left(0, fn($x, $y){$x + $y}),
    "================",
    "$gen2ToInf?take(5)?fold-right(0, fn($x, $y){$x + $y})",
    $gen2ToInf?take(5)?fold-right(0, fn($x, $y){$x + $y}),
    "================",
    "==> $gen0toInf?for-each(fn($n){(2 * $n + 1) div $factorial(2*xs:decimal($n)})
             ?take(8)?fold-left(0, fn($x, $y){$x + $y})",
    $gen0toInf?for-each(fn($n){(2*$n + 1) div $factorial(2*xs:decimal($n))})?take(8)?fold-left(0, fn($x, $y){$x + $y}),
    "$gen0toInf?for-each(fn($n){(2*$n + 1) div $factorial(2*xs:decimal($n))})?take(8)?scan-left(0, fn($x, $y){$x + $y})?to-array()",
    $gen0toInf?for-each(fn($n){(2*$n + 1) div $factorial(2*xs:decimal($n))})?take(8)?scan-left(0, fn($x, $y){$x + $y})?to-array(),
    "let $genSeqE := $gen0toInf?for-each(fn($n){(2*$n + 1) div $factorial(2*xs:decimal($n))})?take(8)?scan-left(0, fn($x, $y){$x + $y}),
    $genSeqE-Next := $genSeqE?tail(),
    $genZipped := $genSeqE?zip($genSeqE-Next)
 return
    $genZipped?first-where(fn($pair){abs($pair(1) - $pair(2)) lt 0.000001})(2)",
    let $genSeqE := $gen0toInf?for-each(fn($n){(2*$n + 1) div $factorial(2*xs:decimal($n))})?take(8)?scan-left(0, fn($x, $y){$x + $y}),
        $genSeqE-Next := $genSeqE?tail(),
        $genZipped := $genSeqE?zip($genSeqE-Next)
      return
        $genZipped?first-where(fn($pair){abs($pair(1) - $pair(2)) lt 0.000001})(2), 
    "================",
    
    "$gen2ToInf?empty-generator()?scan-left(0, fn($x, $y){$x + $y})?to-array()",
    $gen2ToInf?empty-generator()?scan-left(0, fn($x, $y){$x + $y})?to-array(),
    "$gen2ToInf?take(5)?scan-left(0, fn($x, $y){$x + $y})?to-array()",
    $gen2ToInf?take(5)?scan-left(0, fn($x, $y){$x + $y})?to-array(),
    "================",
    "$gen2ToInf?make-generator-from-sequence((1 to 10))?scan-right(0, fn($x, $y){$x + $y})?to-array()",
    $gen2ToInf?make-generator-from-sequence((1 to 10))?scan-right(0, fn($x, $y){$x + $y})?to-array(),
    "================",
    let $multShortCircuitProvider := fn($x, $y)
        {
          if($x eq 0) then fn(){0}
            else fn($z) {$x * $z}
        },
        $gen-5ToInf := $gen2ToInf?for-each(fn($n){$n -7})
     return
     (
       "let $multShortCircuitProvider := fn($x, $y)
        {
          if($x eq 0) then fn(){0}
            else fn($z) {$x * $z}
        },
            $gen-5ToInf := $gen2ToInf?for-each(fn($n){$n -7})
          return
            $gen2ToInf?take(5)?fold-lazy(1, $product, $multShortCircuitProvider),
            $gen-5ToInf?fold-lazy(1, $product, $multShortCircuitProvider)",
       $gen2ToInf?take(5)?fold-lazy(1, $product, $multShortCircuitProvider),
       $gen-5ToInf?fold-lazy(1, $product, $multShortCircuitProvider)
     ),
     "===============",
     "     let $myMap := {'John': 22, 'Ann': 28, 'Peter': 31}
      return 
        $gen2ToInf?make-generator-from-map($myMap)?to-array()",
     let $myMap := {"John": 22, "Ann": 28, "Peter": 31}
      return 
        $gen2ToInf?make-generator-from-map($myMap)?to-array(),
     "===============",        
     "let $myMap := {'John': 22, 'Ann': 28, 'Peter': 31},
          $genMap := $gen2ToInf?make-generator-from-map($myMap)
      return
        $genMap?to-map()" ,
     let $myMap := {"John": 22, "Ann": 28, "Peter": 31},
         $genMap := $gen2ToInf?make-generator-from-map($myMap)
      return
        $genMap?to-map(),
     "$gen2ToInf?take(10)?to-map()",
     $gen2ToInf?take(10)?to-map()     
   )
