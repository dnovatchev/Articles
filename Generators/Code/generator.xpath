declare namespace f = "http://www.w3.org/2005/xpath-functions-2025";
declare record f:generator 
   ( initialized as xs:boolean,
     endReached as xs:boolean,
     getCurrent as %method fn() as item()*,
     moveNext as %method fn(*) (: as f:generator, :),
     to-array := %method fn() as array(*)
     {
       while-do( [., []],
                function( $inArr) 
                { $inArr(1)?initialized and not($inArr(1)?endReached) },                 
                function($inArr) 
                { array{$inArr(1)?moveNext(), 
                        array:append($inArr(2), $inArr(1)?getCurrent())
                       } 
                }         
       ) (2)
     },
     
     take := %method fn($n as xs:integer) as f:generator
     {
      let $gen := if(not(?initialized)) then ?moveNext()
                    else .
       return
         if($gen?endReached or $n le 0) then $gen?empty-generator()
          else
            let $current := $gen?getCurrent(),
                $newResultGen := map:put(., "getCurrent", %method fn(){$current}),
                $nextGen := $gen?moveNext()
             return
               if($nextGen?endReached) then $newResultGen
                 else
                   let
                       $newResultGen2 :=  map:put($newResultGen, "moveNext", %method fn() {$nextGen?take($n -1)}) 
                     return
                       $newResultGen2
      },
      
      take-while := %method fn($pred as function(item()*) as xs:boolean) as f:generator
      {
        let $gen := if(not(?initialized)) then ?moveNext()
                      else .
         return
           if($gen?endReached) then $gen?empty-generator()
            else      
              let $current := $gen?getCurrent()
                return
                  if(not($pred($current))) then $gen?empty-generator()
                  else
                    let $newResultGen := map:put(., "getCurrent", %method fn(){$current}),
                        $nextGen := ?moveNext()
                     return
                        if($nextGen?endReached) then $newResultGen
                        else
                          let $newResultGen2 :=  map:put($newResultGen, "moveNext", %method fn() {$nextGen?take-while($pred)}) 
                           return $newResultGen2  
      },
     
     skip-strict := %method fn($n as xs:nonNegativeInteger, $issueErrorOnEmpty as xs:boolean) as f:generator
     {
            if($n eq 0) then .
              else if(?endReached) 
                     then if($issueErrorOnEmpty)
                           then error((), "Input Generator too-short") 
                           else ?empty-generator()
              else 
                let $gen := if(not(?initialized)) then ?moveNext()
                             else .
                  return
                    if(not($gen?endReached)) then $gen?moveNext()?skip-strict($n -1, $issueErrorOnEmpty)
                      else $gen?empty-generator()                 

     },
     skip := %method fn($n as xs:nonNegativeInteger) as f:generator
     {
       ?skip-strict($n, false())
     },
     
     skip-while := %method fn($pred as function(item()*) as xs:boolean) as f:generator
     {
        let $gen := if(not(?initialized)) then ?moveNext()
                      else .
         return
           if($gen?endReached) then $gen?empty-generator()
            else
              let $current := $gen?getCurrent()
               return
                 if(not($pred($current))) then $gen
                  else $gen?moveNext()?skip-while($pred)                    
     },
     
     some := %method fn() as xs:boolean
     {
       ?initialized and not(?endReached)
     },
     
     some-where := %method fn($pred) as xs:boolean
     {
       ?filter($pred)?some()
     },
     
     first-where := %method fn($pred) as item()*
     {
       ?filter($pred)?head()
     },     
     
     subrange := %method fn($m as xs:positiveInteger, $n as xs:integer) as f:generator
     {
       ?skip($m - 1)?take($n - $m + 1)
     },
     
     chunk := %method fn($size as xs:positiveInteger) as f:generator
     {
        let $gen := if(not(?initialized)) then ?moveNext()
                      else .
         return
           if($gen?endReached) then $gen?empty-generator()
           else
             let $thisChunk := $gen?take($size)?to-array(),
                 $cutGen := $gen?skip($size),
                 $resultGen := $gen => map:put("getCurrent", %method fn(){$thisChunk})
                                    => map:put("moveNext", %method fn(){$cutGen?chunk($size)})
              return $resultGen
     },
     
     head := %method fn() as item()* {?take(1)?getCurrent()},
     tail := %method fn() as f:generator {?skip(1)},
     
     at := %method fn($ind as xs:nonNegativeInteger) as item()* {?subrange($ind, $ind)?getCurrent()},
     
     contains %method fn($value as item()*) as xs:boolean
     {
       let $gen := if(not(?initialized)) then ?moveNext()
                     else .
        return
          if($gen?endReached) then false()
           else
             let $current := $gen?getCurrent()
               return
                  if(deep-equal($current, $value)) then true()
                   else $gen?moveNext()?contains($value)                  
     },
           
     for-each := %method fn($fun as function(*)) as f:generator
     {
      let $gen := if(not(?initialized)) then ?moveNext()
                    else .        
       return
         if(?endReached) then ?empty-generator()
          else
           let $current := $fun(?getCurrent()),
                $newResultGen := map:put(., "getCurrent", %method fn(){$current}),
                $nextGen := ?moveNext()
            return
              if($nextGen?endReached) then $newResultGen
                else
                  let $newResultGen2 :=  map:put($newResultGen, "moveNext", %method fn() {$nextGen?for-each($fun)}) 
                     return
                       $newResultGen2                    
      },
      
      for-each-pair := %method fn($gen2 as f:generator, $fun as function(*)) as f:generator
      {
        let $gen := if(not(?initialized)) then ?moveNext()
                    else .,
            $gen2 := if(not($gen2?initialized)) then $gen2?moveNext()
                    else $gen2
         return
            if(?endReached or $gen2?endReached) then ?empty-generator() 
             else  
               let $current := $fun(?getCurrent(), $gen2?getCurrent()),
                   $newResultGen := map:put(., "getCurrent", %method fn(){$current}),
                   $nextGen1 := ?moveNext(),
                   $nextGen2 := $gen2?moveNext()
                return
                   if($nextGen1?endReached or $nextGen2?endReached) then $newResultGen
                     else
                       let $newResultGen2 := map:put($newResultGen, "moveNext", %method fn(){$nextGen1?for-each-pair($nextGen2, $fun)})
                         return
                           $newResultGen2                        
      },
      
      zip := %method fn($gen2 as f:generator) as f:generator
      {
        ?for-each-pair($gen2, fn($x1, $x2){[$x1, $x2]})
      },

      concat := %method fn($gen2 as f:generator) as f:generator
      {
        let $gen := if(not(?initialized)) then ?moveNext()
                    else .,
            $gen2 := if(not($gen2?initialized)) then $gen2?moveNext()
                    else $gen2,
            $resultGen := if($gen?endReached) then $gen2
                            else if($gen2?endReached) then $gen
                            else
                              $gen  => map:put(  "moveNext", 
                                                %method fn()
                                                 {
                                                 let $nextGen := $gen?moveNext()
                                                   return 
                                                     $nextGen?concat($gen2)
                                                 }
                                              )                                   
        return 
           $resultGen            
      },

      append := %method fn($value as item()*) as f:generator
      {
        let $gen := if(not(?initialized)) then ?moveNext()
                    else .,
            $genSingle := $gen => map:put("getCurrent", %method fn(){$value})
                               => map:put("moveNext", %method fn(){?empty-generator()})
                               => map:put("endReached", false())
         return
           $gen?concat($genSingle)                    
      },
      
      prepend := %method fn($value as item()*) as f:generator
      {
                let $gen := if(not(?initialized)) then ?moveNext()
                    else .,
                    $genSingle := $gen => map:put("getCurrent", %method fn(){$value})
                                       => map:put("moveNext", %method fn(){?empty-generator()})
         return
           $genSingle?concat($gen)  
      },
      
      insert-at := %method fn($pos as xs:positiveInteger, $value as item()*) as f:generator
      {
        let $genTail := ?skip-strict($pos - 1, true())
         return
            if($pos gt 1)
              then ?take($pos - 1)?append($value)?concat($genTail)
              else $genTail?prepend($value)               
      },
      
      remove-at := %method fn($pos as xs:nonNegativeInteger) as f:generator
      {
        let $genTail := ?skip-strict($pos, true())
          return
            if($pos gt 1)
              then ?take($pos - 1)?concat($genTail)
              else $genTail
      },
      
      remove-where := %method fn($predicate as function(item()*) as xs:boolean) as f:generator
      {
        let $gen := if(not(?initialized)) then ?moveNext()
                      else .
          return
            $gen?filter(fn($x){not($predicate($x))})  
      },
      
      distinct %method fn() as f:generator
      {
        let $gen := if(not(?initialized)) then ?moveNext()
                      else .
         return
           if($gen?endReached) then $gen
           else
             let $priorValue := ?getCurrent()
               return
                 $gen => map:put("moveNext", %method fn(){$gen?tail()?remove-where(fn($x){deep-equal($priorValue, $x)})?distinct()})  
      },
    
      replace := %method fn($funIsMatching as function(item()*) as xs:boolean, $replacement as item()*) as f:generator
      {
        if(?endReached) then .
          else
            let $current := ?getCurrent()
              return
                if($funIsMatching($current))
                  then let $nextGen := ?moveNext()
                     return
                       . => map:put("getCurrent", %method fn() {$replacement})
                         => map:put("moveNext", %method fn() { $nextGen } 
                                  )
                  else (: $current is not the match for replacement :)
                    let $nextGen := ?moveNext()
                      return . => map:put("moveNext", 
                                           %method fn()
                                           {
                                             let $intendedReplace := function($z) {$z?replace($funIsMatching, $replacement)}
                                              return
                                                if($nextGen?endReached) then $nextGen
                                                else $intendedReplace($nextGen)
                                           }
                                        )
      },
      
      reverse := %method fn() as f:generator
      {
        if(?endReached) then ?empty-generator()
          else
           let $current := ?getCurrent()
             return
               ?tail()?reverse()?append($current)
      },

      filter := %method fn($pred as function(item()*) as xs:boolean) as f:generator
      {
             if(?initialized and ?endReached) then ?empty-generator()
              else
                let $getNextGoodGen := function($gen as map(*), 
                                             $pred as function(item()*) as xs:boolean)
                   {
                      if($gen?endReached) then $gen?empty-generator()
                      else
                        let $mapResult := 
                              while-do(
                                       $gen,
                                       function($x) { not($x?endReached) and not($pred($x?getCurrent()))},
                                       function($x) { $x?moveNext() }
                                       )   
                        return 
                          if($mapResult?endReached) then $gen?empty-generator()
                           else $mapResult                  
                   },
                   
                   $gen := if(?initialized) then . 
                             else ?moveNext(),
                   $nextGoodGen := $getNextGoodGen($gen, $pred)
                return
                  if($nextGoodGen?endReached) then $gen?empty-generator()
                  else
                    $nextGoodGen => map:put("moveNext", 
                                            %method fn() 
                                              {
                                                let $nextGoodGen := $getNextGoodGen(?inputGen?moveNext(), $pred)
                                                  return
                                                    if($nextGoodGen?endReached) then $nextGoodGen?empty-generator()
                                                    else
                                                      map:put(map:put($nextGoodGen, "moveNext", %method fn() {$nextGoodGen?moveNext()?filter($pred)}),
                                                                      "inputGen", $nextGoodGen
                                                              )
                                               }
                                           )
                                   =>
                                     map:put("inputGen", $nextGoodGen)
        },     
        fold-left := %method fn($init as item()*, $action as fn(*)) as item()*
        {
          if(?endReached) then $init
            else ?tail()?fold-left($action($init, ?getCurrent()), $action)
        },
        
        fold-right := %method fn($init as item()*, $action as fn(*)) as item()*
        {
          if(?endReached) then $init
            else $action(?head(), ?tail()?fold-right($init, $action))
        },
        
        fold-lazy := %method fn($init as item()*, $action as fn(*), $shortCircuitProvider as function(*)) as item()*
        {
          if(?endReached) then $init
          else
           let $current := ?getCurrent()
             return
               if(function-arity($shortCircuitProvider($current, $init)) eq 0)
                 then $shortCircuitProvider($current, $init)()
                 else $action($current, ?moveNext()?fold-lazy($init, $action, $shortCircuitProvider))
        },
        
        scan-left := %method fn($init as item()*, $action as fn(*)) as f:generator
        {
          let $resultGen := ?empty-generator() 
                                => map:put("endReached", false())
                                => map:put("getCurrent", %method fn(){$init})
           return
             if(?endReached) 
               then $resultGen => map:put("moveNext", %method fn(){?empty-generator()})
               else
                 let $resultGen := $resultGen => map:put("getCurrent", %method fn(){$init}),
                     $partialFoldResult := $action($init, ?getCurrent())
                   return
                     let $nextGen := ?moveNext()
                      return
                        $resultGen => map:put("moveNext", %method fn()
                                              { 
                                                  $nextGen?scan-left($partialFoldResult, $action)
                                               }
                                              )            
        },
      
        scan-right := %method fn($init as item()*, $action as fn(*)) as f:generator
        {
          ?reverse()?scan-left($init, $action)?reverse()                         
        },
        
        make-generator := %method fn($provider as function(*)) as f:generator
        {
         let $gen := if(not(?initialized)) then ?moveNext()
                    else .,
              $nextDataItemGetter := $provider(0),
              $nextGen := if(not($nextDataItemGetter instance of function(*))) then $gen?empty-generator()  
                           else $gen?empty-generator()
                            => map:put("numDataItems", 1)
                            => map:put("current", $nextDataItemGetter())
                            => map:put("endReached", false())
                            => map:put("getCurrent", %method fn() {?current})
                            => map:put("moveNext",  
                                       %method fn() 
                                        {
                                          let $nextDataItemGetter := $provider(?numDataItems)
                                            return
                                              if(not($nextDataItemGetter instance of function(*))) then ?empty-generator()
                                              else
                                                . => map:put("current", $nextDataItemGetter())
                                                  => map:put("numDataItems", ?numDataItems + 1)
                                        }
                                       )
           return $nextGen                                                  
        },
        
        make-generator-from-array := %method fn($input as array(*)) as f:generator
        {
          let $size := array:size($input),
              $arrayProvider := fn($ind as xs:integer)
                                {
                                  if($ind +1 gt $size) then -1
                                   else fn(){$input($ind + 1)}
                                }
           return ?make-generator($arrayProvider)
        },
        
        make-generator-from-sequence := %method fn($input as item()*) as f:generator
        {
          let $size := count($input),
              $seqProvider := fn($ind as xs:integer)
                                {
                                  if($ind +1 gt $size) then -1
                                   else fn(){$input[$ind + 1]}
                                }
           return ?make-generator($seqProvider)
        },
        
        make-generator-from-map := %method fn($inputMap as map(*)) as f:generator
        {
          let $keys := map:keys($inputMap),
              $size := map:size($inputMap),
              $mapProvider := fn($ind as xs:integer)
              {
                if($ind +1 gt $size) then -1
                  else fn() 
                       {
                         let $key := $keys[$ind + 1]
                          return
                            ( $key, [ $inputMap($key) ])
                       }
              }
            return
              ?make-generator($mapProvider)
        },
        
        to-sequence := %method fn() as item()* {?to-array() => array:items()},     
        
        to-map := %method fn() as map(*)
        {
          let $genPairs := ?for-each(fn($x)
                           {
                             let $key := head($x),
                                 $tail := tail($x),
                                 $value := if($tail instance of array(*)) 
                                           then for $ind in 1 to array:size($tail)
                                                 return $tail($ind)
                                           else $tail
                             return
                               map:pair($key, $value)
                           }
                         ) 
           return
             map:of-pairs($genPairs?to-sequence())

        },
        
        empty-generator := %method fn() as f:generator
        {
          . => map:put("initialized", true()) => map:put("endReached", true())
            => map:put("getCurrent", %method fn() {error((),"getCurrent() called on an empty-generator")})
            => map:put("moveNext", %method fn() {error((),"moveNext() called on an empty-generator")})
        },      
     *
   );

let $gen2ToInf := f:generator(initialized := true(), endReached := false(), 
                              getCurrent := %method fn(){?last +1},
                              moveNext := %method fn()
                              {
                                if(not(?initialized))
                                  then map:put(., "initialized", true())
                                  else map:put(., "last", ?last + 1)
                              },
                              options := {"last" : 1}
                             ),
    $genN := $gen2ToInf?for-each(fn($n) {$n - 1}),
    $gen0toInf := $gen2ToInf?for-each(fn($n) {$n - 2}),
    $double := fn($n) {2*$n},
    $sum2 := fn($m, $n) {$m + $n},
    $product := fn($m, $n) {$m * $n},
    $factorial := fn($n) {fold-left(1 to $n, 1, $product)}
  return    
  (
    "$gen2ToInf?take(3)?to-array()",
    $gen2ToInf?take(3)?to-array(),
    "================",    
    "$gen2ToInf?take(3)?skip(2)?getCurrent()",
    $gen2ToInf?take(3)?skip(2)?getCurrent(),
    (: $gen2ToInf?take(3)?moveNext()?moveNext()?moveNext()?getCurrent(), :)
    "================",
    "$gen2ToInf?getCurrent()",
    $gen2ToInf?getCurrent(),
    "$gen2ToInf?moveNext()?getCurrent()",
    $gen2ToInf?moveNext()?getCurrent(),
    "================",
    "$gen2ToInf?take(5) instance of f:generator",
    $gen2ToInf?take(5) instance of f:generator,
    "==>  $gen2ToInf?skip(7) instance of f:generator",
    $gen2ToInf?skip(7) instance of f:generator,  
    "================",
    "$gen2ToInf?subrange(4, 6)?getCurrent()",
    $gen2ToInf?subrange(4, 6)?getCurrent(), 
    "$gen2ToInf?subrange(4, 6)?moveNext()?getCurrent()",
    $gen2ToInf?subrange(4, 6)?moveNext()?getCurrent(),
    "$gen2ToInf?subrange(4, 6)?moveNext()?moveNext()?getCurrent()",
    $gen2ToInf?subrange(4, 6)?moveNext()?moveNext()?getCurrent(),
    (: $gen2ToInf?subrange(4, 6)?moveNext()?moveNext()?moveNext()?getCurrent() :) (: Must raise error:)    
    "================",    
    "$gen2ToInf?subrange(4, 6)?head()",
    $gen2ToInf?subrange(4, 6)?head(),  
    "$gen2ToInf?subrange(4, 6)?tail()?head()",
    $gen2ToInf?subrange(4, 6)?tail()?head(),
    "$gen2ToInf?subrange(4, 6)?to-array()",
    $gen2ToInf?subrange(4, 6)?to-array(),
    "$gen2ToInf?head()",
    $gen2ToInf?head(),
    "==>  $gen2ToInf?tail()?head()",
    $gen2ToInf?tail()?head(),
    "================", 
    "$gen2ToInf?subrange(4, 6)?tail()?to-array()",
    $gen2ToInf?subrange(4, 6)?tail()?to-array(),
    "================",
    "$gen2ToInf?at(5)",
    $gen2ToInf?at(5), 
    "================",
    "$gen2ToInf?subrange(1, 5)?to-array()",
    $gen2ToInf?subrange(1, 5)?to-array(),
    "$gen2ToInf?subrange(1, 5)?for-each($double)?to-array()",
    $gen2ToInf?subrange(1, 5)?for-each($double)?to-array(),
    "$gen2ToInf?take(5)?for-each($double)?to-array()",
    $gen2ToInf?take(5)?for-each($double)?to-array(),
    "==>  $gen2ToInf?for-each($double)?take(5)?to-array()",
    $gen2ToInf?for-each($double)?take(5)?to-array(),
    "================",
    "$gen2ToInf?subrange(1, 5)?to-array()",
    $gen2ToInf?subrange(1, 5)?to-array(),
    "$gen2ToInf?subrange(6, 10)?to-array()",
    $gen2ToInf?subrange(6, 10)?to-array(),
    "$gen2ToInf?subrange(1, 5)?for-each-pair($gen2ToInf?subrange(6, 10), $sum2)?to-array()",
    $gen2ToInf?subrange(1, 5)?for-each-pair($gen2ToInf?subrange(6, 10), $sum2)?to-array(), 
    "==>  $gen2ToInf?for-each-pair($gen2ToInf, $sum2)?take(5)?to-array()",
    $gen2ToInf?for-each-pair($gen2ToInf, $sum2)?take(5)?to-array(),
    "================",
    "==>  $gen2ToInf?filter(fn($n){$n mod 2 eq 1})?getCurrent()",
    $gen2ToInf?filter(fn($n){$n mod 2 eq 1})?getCurrent(),
    "$gen2ToInf?filter(fn($n){$n mod 2 eq 1})?moveNext()?getCurrent()",
    $gen2ToInf?filter(fn($n){$n mod 2 eq 1})?moveNext()?getCurrent(),
    "================", 
    "$gen2ToInf?filter(fn($n){$n mod 2 eq 1})?take(10)?to-array()",
    $gen2ToInf?filter(fn($n){$n mod 2 eq 1})?take(10)?to-array(),  
    "================", 
    "$gen2ToInf?filter(fn($n){$n mod 2 eq 1})?take(10)?to-sequence()",
    $gen2ToInf?filter(fn($n){$n mod 2 eq 1})?take(10)?to-sequence(),
    "================", 
    "$gen2ToInf?take-while(fn($n){$n lt 11})?to-array()",
    $gen2ToInf?take-while(fn($n){$n lt 11})?to-array(), 
    "$gen2ToInf?take-while(fn($n){$n lt 2})?to-array()",
    $gen2ToInf?take-while(fn($n){$n lt 2})?to-array(), 
    "================", 
    "$gen2ToInf?skip-while(fn($n){$n lt 11})?take(5)?to-array()",
    $gen2ToInf?skip-while(fn($n){$n lt 11})?take(5)?to-array(),
    "==> $gen2ToInf?skip-while(fn($n){$n lt 2})",
    $gen2ToInf?skip-while(fn($n){$n lt 2}),
    "
     ==> $gen2ToInf?skip-while(fn($n){$n lt 2})?skip(1)",
    $gen2ToInf?skip-while(fn($n){$n lt 2})?skip(1),
(:    $gen2ToInf?skip-while(fn($x) {$x ge 2}) :) (: ?skip(1) :) 
    "$gen2ToInf?some()",
     $gen2ToInf?some(),
     "let $empty := $gen2ToInf?empty-generator()
      return $empty?some()",
     let $empty := $gen2ToInf?empty-generator()
      return $empty?some(),
    "================",
    "$gen2ToInf?take(5)?filter(fn($n){$n ge 7})?some()",
     $gen2ToInf?take(5)?filter(fn($n){$n ge 7})?some(),  
     "$gen2ToInf?take(5)?some-where(fn($n){$n ge 7})",
     $gen2ToInf?take(5)?some-where(fn($n){$n ge 7}), 
     "$gen2ToInf?take(5)?some-where(fn($n){$n ge 6})",
     $gen2ToInf?take(5)?some-where(fn($n){$n ge 6}),
     "$gen2ToInf?some-where(fn($n){$n ge 100})",
     $gen2ToInf?some-where(fn($n){$n ge 100}),
     "================",
     "$gen2ToInf?take(10)?take(11)?to-array()",
     $gen2ToInf?take(10)?take(11)?to-array(),
     "$gen2ToInf?take(10)?skip(10)?to-array()",
     $gen2ToInf?take(10)?skip(10)?to-array(),
     "$gen2ToInf?take(10)?skip(9)?to-array()",     
     $gen2ToInf?take(10)?skip(9)?to-array(),
     "$gen2ToInf?take(10)?subrange(3, 12)?to-array()",
     $gen2ToInf?take(10)?subrange(3, 12)?to-array(),
     "$gen2ToInf?take(10)?subrange(5, 3)?to-array()",
     $gen2ToInf?take(10)?subrange(5, 3)?to-array(),
     "================",
     "$gen2ToInf?take(100)?chunk(20)?getCurrent()",
      $gen2ToInf?take(100)?chunk(20)?getCurrent(),
      "==>  $gen2ToInf?chunk(20)?take(5)?to-array()",
      $gen2ToInf?chunk(20)?take(5)?to-array(),
     "================",
     "$gen2ToInf?take(100)?chunk(20)?moveNext()?getCurrent()",
      $gen2ToInf?take(100)?chunk(20)?moveNext()?getCurrent(),
     "$gen2ToInf?take(100)?chunk(20)?moveNext()?moveNext()?getCurrent()", 
      $gen2ToInf?take(100)?chunk(20)?moveNext()?moveNext()?getCurrent(),
     "$gen2ToInf?take(100)?chunk(20)?skip(1)?getCurrent()",      
      $gen2ToInf?take(100)?chunk(20)?skip(1)?getCurrent(),
     "================",      
     "$gen2ToInf?take(100)?chunk(20)?for-each(fn($genX){$genX})?to-array()",      
      $gen2ToInf?take(100)?chunk(20)?for-each(fn($genX){$genX})?to-array(),
     "================",  
     "$gen2ToInf?take(10)?chunk(4)?to-array()",
      $gen2ToInf?take(10)?chunk(4)?to-array(),
      "$gen2ToInf?take(10)?chunk(4)?for-each(fn($arr){array:size($arr)})?to-array()",
      $gen2ToInf?take(10)?chunk(4)?for-each(fn($arr){array:size($arr)})?to-array(),
     "================", 
     "$gen2ToInf?subrange(10, 15)?concat($gen2ToInf?subrange(1, 9))?to-array()",
     $gen2ToInf?subrange(10, 15)?concat($gen2ToInf?subrange(1, 9))?to-array(),
     "================", 
     "$gen2ToInf?subrange(1, 5)?append(101)?to-array()",
     $gen2ToInf?subrange(1, 5)?append(101)?to-array(),
     "$gen2ToInf?subrange(1, 5)?prepend(101)?to-array()",
     $gen2ToInf?subrange(1, 5)?prepend(101)?to-array(),
     "==>  $gen2ToInf?append(101)",
     $gen2ToInf?append(101),
     "$gen2ToInf?prepend(101)?take(5)?to-array()",
     $gen2ToInf?prepend(101)?take(5)?to-array(),
     "================", 
     "$gen2ToInf?subrange(1, 5)?zip($gen2ToInf?subrange(6, 10))?to-array()",
     $gen2ToInf?subrange(1, 5)?zip($gen2ToInf?subrange(6, 10))?to-array(),
     "$gen2ToInf?subrange(1, 5)?zip($gen2ToInf?subrange(10, 20))?to-array()",
     $gen2ToInf?subrange(1, 5)?zip($gen2ToInf?subrange(10, 20))?to-array(),
     "==>  $gen2ToInf?zip($gen2ToInf?skip(5))?take(10)?to-array()",
     $gen2ToInf?zip($gen2ToInf?skip(5))?take(10)?to-array(),
     "$gen2ToInf?subrange(1, 5)?zip($gen2ToInf?subrange(10, 20))?zip($gen2ToInf?subrange(30, 40))?to-array()",
     $gen2ToInf?subrange(1, 5)?zip($gen2ToInf?subrange(10, 20))?zip($gen2ToInf?subrange(30, 40))?to-array(),
     "================", 
     "$gen2ToInf?make-generator(fn($numGenerated as xs:integer)
                                 {if($numGenerated le 9) then fn() {$numGenerated + 1} else -1} 
                             )?to-array()",
     $gen2ToInf?make-generator(fn($numGenerated as xs:integer)
                                 {if($numGenerated le 9) then fn() {$numGenerated + 1} else -1} 
                             )?to-array(),
     "================", 
     "$gen2ToInf?make-generator-from-array([1, 4, 9, 16, 25])?to-array()",
      $gen2ToInf?make-generator-from-array([1, 4, 9, 16, 25])?to-array(),
      "$gen2ToInf?make-generator-from-sequence((1, 8, 27, 64, 125))?to-array()",
      $gen2ToInf?make-generator-from-sequence((1, 8, 27, 64, 125))?to-array(), 
     "================", 
     "$gen2ToInf?take(10)?insert-at(3, ""XYZ"")?to-array()",
      $gen2ToInf?take(10)?insert-at(3, "XYZ")?to-array(),
      "$gen2ToInf?take(10)?insert-at(1, ""ABC"")?to-array()",
      $gen2ToInf?take(10)?insert-at(1, "ABC")?to-array(),
      "$gen2ToInf?take(10)?insert-at(11, ""PQR"")?to-array()",
      $gen2ToInf?take(10)?insert-at(11, "PQR")?to-array(),
      "==>  $gen2ToInf?insert-at(3, ""XYZ"")?take(10)?to-array()", 
      $gen2ToInf?insert-at(3, "XYZ")?take(10)?to-array(),
     (: , $gen2ToInf?take(10)?insert-at(12, "GHI")?to-array() :)  (:  Must raise error "Input Generator too-short." :) 
     "================", 
     "$gen2ToInf?take(10)?remove-at(3)?to-array()",
      $gen2ToInf?take(10)?remove-at(3)?to-array(),
      "$gen2ToInf?take(10)?remove-at(1)?to-array()",
      $gen2ToInf?take(10)?remove-at(1)?to-array(),
      "$gen2ToInf?take(10)?remove-at(10)?to-array()",
      $gen2ToInf?take(10)?remove-at(10)?to-array(),
      "==>  $gen2ToInf?remove-at(3)?take(10)?to-array()",
      $gen2ToInf?remove-at(3)?take(10)?to-array(),
      (: , $gen2ToInf?take(10)?remove-at(11)?to-array() :)        (:  Must raise error "Input Generator too-short." :) 
     "================",
     "==>  $gen2ToInf?remove-where(fn($x){$x mod 3 eq 0})?take(10)?to-array()",
      $gen2ToInf?remove-where(fn($x){$x mod 3 eq 0})?take(10)?to-array(),    
     "================",
     "$gen2ToInf?make-generator-from-sequence((1,  3, 1, 2,  1, 2, 5, 2, 5))?distinct()?to-array()",
      $gen2ToInf?make-generator-from-sequence((1,  3, 1, 2,  1, 2, 5, 2, 5))?distinct()?to-array(),
      "$gen2ToInf?for-each(fn($n){$n idiv 10})?take(50)?distinct()?to-array()",
      $gen2ToInf?for-each(fn($n){$n idiv 10})?take(50)?distinct()?to-array(),
      "$gen2ToInf?for-each(fn($n){$n idiv 10})?take(100)?distinct()?to-array()",
      $gen2ToInf?for-each(fn($n){$n idiv 10})?take(100)?distinct()?to-array(),
      "==> $gen2ToInf?for-each(fn($n){$n idiv 10})?distinct()?take(100)?to-array()",
      $gen2ToInf?for-each(fn($n){$n idiv 10})?distinct()?take(100)?to-array(),
     "================",          
     "$gen2ToInf?take(10)?replace(fn($x){$x gt 4}, ""Replacement"")?to-array()",
      $gen2ToInf?take(10)?replace(fn($x){$x gt 4}, "Replacement")?to-array(),
      "$gen2ToInf?take(10)?replace(fn($x){$x lt 3}, ""Replacement"")?to-array()",
      $gen2ToInf?take(10)?replace(fn($x){$x lt 3}, "Replacement")?to-array(),
      "$gen2ToInf?take(10)?replace(fn($x){$x gt 10}, ""Replacement"")?to-array()",
      $gen2ToInf?take(10)?replace(fn($x){$x gt 10}, "Replacement")?to-array(),
      "$gen2ToInf?take(10)?replace(fn($x){$x gt 11}, ""Replacement"")?to-array()",
      $gen2ToInf?take(10)?replace(fn($x){$x gt 11}, "Replacement")?to-array(),
      "$gen2ToInf?take(10)?replace(fn($x){$x lt 2}, ""Replacement"")?to-array()",
      $gen2ToInf?take(10)?replace(fn($x){$x lt 2}, "Replacement")?to-array(),
      "==> $gen2ToInf?replace(fn($x){$x gt 4}, ""Replacement"")?take(10)?to-array()",
      $gen2ToInf?replace(fn($x){$x gt 4}, "Replacement")?take(10)?to-array(),
      "$gen2ToInf?replace(fn($x){$x lt 3}, ""Replacement"")?take(10)?to-array()",
      $gen2ToInf?replace(fn($x){$x lt 3}, "Replacement")?take(10)?to-array(),
    (:  
      Will result in endless loop:
      
      , "==>  ==>  ==>  $gen2ToInf?replace(fn($x){$x lt 2}, ""Replacement"")?take(10)?to-array() <==  <==  <==",
      $gen2ToInf?replace2(fn($x){$x lt 2}, "Replacement")?take(10)?to-array() 
    :)
    "================",
    "$gen2ToInf?empty-generator()?reverse()?to-array()",
    $gen2ToInf?empty-generator()?reverse()?to-array(),
    "$gen2ToInf?empty-generator()?append(2)?reverse()?to-array()",
    $gen2ToInf?empty-generator()?append(2)?reverse()?to-array(),
    "$gen2ToInf?take(10)?reverse()?to-array()",
    $gen2ToInf?take(10)?reverse()?to-array(),
    "================",
    "$genN?take(10)?contains(3)",
    $genN?take(10)?contains(3),
    "$genN?take(10)?contains(20)",
    $genN?take(10)?contains(20),
    "$genN?take(10)?contains(1)",    
    $genN?take(10)?contains(1), 
    "$genN?take(10)?contains(10)",     
    $genN?take(10)?contains(10),  
    "$genN?take(10)?contains(0)",
    $genN?take(10)?contains(0), 
    "$genN?take(10)?contains(11)",        
    $genN?take(10)?contains(11),
    "==> $genN?contains(15)",    
    $genN?contains(15), 
    "================",
    "$gen2ToInf?take(5)?fold-left(0, fn($x, $y){$x + $y})",
    $gen2ToInf?take(5)?fold-left(0, fn($x, $y){$x + $y}),
    "================",
    "$gen2ToInf?take(5)?fold-right(0, fn($x, $y){$x + $y})",
    $gen2ToInf?take(5)?fold-right(0, fn($x, $y){$x + $y}),
    "================",
    "==> $gen0toInf?for-each(fn($n){(2 * $n + 1) div $factorial(2*xs:decimal($n)})
             ?take(8)?fold-left(0, fn($x, $y){$x + $y})",
    $gen0toInf?for-each(fn($n){(2*$n + 1) div $factorial(2*xs:decimal($n))})?take(8)?fold-left(0, fn($x, $y){$x + $y}),
    "$gen0toInf?for-each(fn($n){(2*$n + 1) div $factorial(2*xs:decimal($n))})?take(8)?scan-left(0, fn($x, $y){$x + $y})?to-array()",
    $gen0toInf?for-each(fn($n){(2*$n + 1) div $factorial(2*xs:decimal($n))})?take(8)?scan-left(0, fn($x, $y){$x + $y})?to-array(),
    "let $genSeqE := $gen0toInf?for-each(fn($n){(2*$n + 1) div $factorial(2*xs:decimal($n))})?take(8)?scan-left(0, fn($x, $y){$x + $y}),
    $genSeqE-Next := $genSeqE?tail(),
    $genZipped := $genSeqE?zip($genSeqE-Next)
 return
    $genZipped?first-where(fn($pair){abs($pair(1) - $pair(2)) lt 0.000001})(2)",
    let $genSeqE := $gen0toInf?for-each(fn($n){(2*$n + 1) div $factorial(2*xs:decimal($n))})?take(8)?scan-left(0, fn($x, $y){$x + $y}),
        $genSeqE-Next := $genSeqE?tail(),
        $genZipped := $genSeqE?zip($genSeqE-Next)
      return
        $genZipped?first-where(fn($pair){abs($pair(1) - $pair(2)) lt 0.000001})(2), 
    "================",
    
    "$gen2ToInf?empty-generator()?scan-left(0, fn($x, $y){$x + $y})?to-array()",
    $gen2ToInf?empty-generator()?scan-left(0, fn($x, $y){$x + $y})?to-array(),
    "$gen2ToInf?take(5)?scan-left(0, fn($x, $y){$x + $y})?to-array()",
    $gen2ToInf?take(5)?scan-left(0, fn($x, $y){$x + $y})?to-array(),
    "================",
    "$gen2ToInf?make-generator-from-sequence((1 to 10))?scan-right(0, fn($x, $y){$x + $y})?to-array()",
    $gen2ToInf?make-generator-from-sequence((1 to 10))?scan-right(0, fn($x, $y){$x + $y})?to-array(),
    "================",
    let $multShortCircuitProvider := fn($x, $y)
        {
          if($x eq 0) then fn(){0}
            else fn($z) {$x * $z}
        },
        $gen-5ToInf := $gen2ToInf?for-each(fn($n){$n -7})
     return
     (
       "let $multShortCircuitProvider := fn($x, $y)
        {
          if($x eq 0) then fn(){0}
            else fn($z) {$x * $z}
        },
            $gen-5ToInf := $gen2ToInf?for-each(fn($n){$n -7})
          return
            $gen2ToInf?take(5)?fold-lazy(1, $product, $multShortCircuitProvider),
            $gen-5ToInf?fold-lazy(1, $product, $multShortCircuitProvider)",
       $gen2ToInf?take(5)?fold-lazy(1, $product, $multShortCircuitProvider),
       $gen-5ToInf?fold-lazy(1, $product, $multShortCircuitProvider)
     ),
     "===============",
     "     let $myMap := {'John': 22, 'Ann': 28, 'Peter': 31}
      return 
        $gen2ToInf?make-generator-from-map($myMap)?to-array()",
     let $myMap := {"John": 22, "Ann": 28, "Peter": 31}
      return 
        $gen2ToInf?make-generator-from-map($myMap)?to-array(),
     "===============",        
     "let $myMap := {'John': 22, 'Ann': 28, 'Peter': 31},
          $genMap := $gen2ToInf?make-generator-from-map($myMap)
      return
        $genMap?to-map()" ,
     let $myMap := {"John": 22, "Ann": 28, "Peter": 31},
         $genMap := $gen2ToInf?make-generator-from-map($myMap)
      return
        $genMap?to-map(),
     "$gen2ToInf?take(10)?to-map()",
     $gen2ToInf?take(10)?to-map()     
   )
